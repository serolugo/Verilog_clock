/*************** Contador de 0 a 59 con preset ****************/

module bcd_counter_00_59 #(
    parameter integer INIT_VAL = 0  // 0..59
)(
    input  wire clk,
    input  wire rst,    // activo en ALTO, síncrono
    input  wire en,
    output reg  [3:0] tens,
    output reg  [3:0] ones,
    output wire carry
);
    // Convertimos el valor inicial decimal a BCD (tiempo de síntesis, no en hardware)
    localparam [3:0] INIT_TENS = INIT_VAL / 10;
    localparam [3:0] INIT_ONES = INIT_VAL % 10;

    assign carry = en && (tens == 4'd5) && (ones == 4'd9);

    always @(posedge clk) begin
        if (rst) begin
            tens <= INIT_TENS;
            ones <= INIT_ONES;
        end else if (en) begin
            if (ones == 4'd9) begin
                ones <= 4'd0;
                if (tens == 4'd5) begin
                    tens <= 4'd0;
                end else begin
                    tens <= tens + 1'b1;
                end
            end else begin
                ones <= ones + 1'b1;
            end
        end
    end
endmodule
